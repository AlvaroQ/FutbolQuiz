apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'// Added Firebase Crashlytics plugin.

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildtoolsVersion

    defaultConfig {
        applicationId names.applicationId
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.code
        versionName versions.name
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            jniDebuggable true
            testCoverageEnabled true
            debuggable true
            minifyEnabled false
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
            resValue 'string', 'BANNER_GAME', getAdmobBannerTestId()
            resValue 'string', 'BANNER_RANKING', getAdmobBannerTestId()
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
            resValue 'string', 'BANNER_GAME', getAdmobBannerGameId()
            resValue 'string', 'BANNER_RANKING', getAdmobBannerRankingId()
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation project(":data")
    implementation project(":domain")
    implementation project(":usecases")

    implementation androidLibs.values()
    implementation androidKtxLibs.values()
    implementation kotlinLibs.values()
    implementation libs.values()
    implementation androidKaptLibs.values()

    testImplementation testLibs.values()
    testImplementation androidxTestLibs.values()
    androidTestImplementation androidTestLibs.values()
}

// KEYS ADMOB PRODUCTION
def getAdmobBannerGameId() {
    try {
        def stringsFile = file("./src/main/res/values/secrets.xml")
        return new XmlParser().parse(stringsFile).string.find { it.@name.equals 'admob_banner_game' }.text()
    } catch(e) {
        println(e)
        return ""
    }
}
def getAdmobBannerRankingId() {
    try {
        def stringsFile = file("./src/main/res/values/secrets.xml")
        return new XmlParser().parse(stringsFile).string.find { it.@name.equals 'admob_banner_ranking' }.text()
    } catch(e) {
        println(e)
        return ""
    }
}
// KEYS ADMOB TEST
def getAdmobBannerTestId() {
    try {
        def stringsFile = file("./src/main/res/values/secrets.xml")
        return new XmlParser().parse(stringsFile).string.find { it.@name.equals 'admob_banner_test_id' }.text()
    } catch(e) {
        println(e)
        return ""
    }
}